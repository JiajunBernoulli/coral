buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
  }
}

plugins {
  id 'checkstyle'
  id "org.shipkit.java" version "2.3.4"
}

configurations {
  provided
}

allprojects {
  group = "com.linkedin.coral"
  repositories {
    mavenCentral()
    maven {
      url 'https://linkedin.bintray.com/maven/'
    }
  }
}

subprojects {
  apply plugin: "com.github.hierynomus.license"

  license {
    header = file("${rootDir}/gradle/license/LICENSE_HEADER")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders true
    ignoreFailures false
    strictCheck true

    exclude '**/*.avsc'
    exclude '**/*.json'
  }

  apply plugin: 'checkstyle'
  checkstyle {
    configFile = file("${rootDir}/gradle/checkstyle/checkstyle.xml")
    toolVersion = '8.11'
  }

  plugins.withType(JavaPlugin) {
    dependencies {
      testCompile deps.'testing'
    }

    test {
      useTestNG()
    }
  }

  apply from: "${rootDir}/gradle/dependencies.gradle"
}
